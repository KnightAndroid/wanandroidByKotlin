//App壳配置文件
import com.knight.wanandroid.buildsrc.*
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.alibaba.arouter'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'


android {
    compileSdkVersion ProjectBuilddencies.compileSdkVersion
    buildToolsVersion ProjectBuilddencies.buildToolsVersion

    defaultConfig {
        applicationId ProjectBuilddencies.applicationId
        minSdkVersion ProjectBuilddencies.minSdkVersion
        targetSdkVersion ProjectBuilddencies.targetSdkVersion
        versionCode ProjectBuilddencies.versionCode
        versionName ProjectBuilddencies.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    //签名配置
    signingConfigs {
        debug{

        }

        release {
            storeFile file('../wanandroid.jks')//密钥文件路径
            storePassword "wanandroid"//密钥文件密码
            keyAlias "wanandroid"//key别名
            keyPassword "wanandroid"//key密码
        }

    }

    buildTypes {
        release {
            //代码混淆开关
            minifyEnabled true
            //移除无用资源
            shrinkResources true
            //压缩对齐开关
            zipAlignEnabled true
            //混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名信息配置
            signingConfig signingConfigs.release
        }
    }
    // 自定义打包apk的文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName != null && outputFileName.endsWith('.apk')) {
                outputFileName = "${ProjectBuilddencies.applicationId}" +
                        "_${ProjectBuilddencies.versionCode}" +
                        "(${ProjectBuilddencies.versionName})" +
                        "_${variant.buildType.name}" +
                        ".apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }


}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path:":module_welcome")
    implementation project(path:":module_home")
    implementation project(path:":module_main")
    implementation project(path:":module_mine")
    implementation project(path:":module_navigate")
    implementation project(path:":module_project")
    implementation project(path:":module_square")
    implementation Dependencies.JetPack.HiltCore
    kapt Dependencies.JetPack.HiltApt
    kapt Dependencies.JetPack.HiltAndroidx
}