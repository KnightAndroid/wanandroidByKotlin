//App壳配置文件
import com.knight.wanandroid.buildsrc.*
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.alibaba.arouter'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'android-aspectjx'


android {
    compileSdkVersion ProjectBuildDencies.compileSdkVersion
    buildToolsVersion ProjectBuildDencies.buildToolsVersion

    defaultConfig {
        applicationId ProjectBuildDencies.applicationId
        minSdkVersion ProjectBuildDencies.minSdkVersion
        targetSdkVersion ProjectBuildDencies.targetSdkVersion
        versionCode ProjectBuildDencies.versionCode
        versionName ProjectBuildDencies.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    //签名配置
    signingConfigs {
        debug{

        }

        release {
            storeFile file('../wanandroid.jks')//密钥文件路径
            storePassword "wanandroid"//密钥文件密码
            keyAlias "wanandroid"//key别名
            keyPassword "wanandroid"//key密码
        }

    }




    buildTypes {

        debug {
            //代码混淆开关
            minifyEnabled false
            //移除无用资源
            shrinkResources false
            //压缩对齐开关
            zipAlignEnabled false
            //混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名信息配置
            signingConfig signingConfigs.release
        }

        release {
            //代码混淆开关
            minifyEnabled true
            //移除无用资源
            shrinkResources true
            //压缩对齐开关
            zipAlignEnabled true
            //混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名信息配置
            signingConfig signingConfigs.release
        }
        master {
            //代码混淆开关
            minifyEnabled true
            //移除无用资源
            shrinkResources true
            //压缩对齐开关
            zipAlignEnabled true
            //混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //签名信息配置
            signingConfig signingConfigs.release
        }
    }
    // 自定义打包apk的文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName != null && outputFileName.endsWith('.apk')) {
                outputFileName = "${ProjectBuildDencies.applicationId}" +
                        "_${ProjectBuildDencies.versionCode}" +
                        "(${ProjectBuildDencies.versionName})" +
                        "_${variant.buildType.name}" +
                        ".apk"
            }
        }
    }

    aspectjx {
        // 排除一些第三方库的包名（Gson、 LeakCanary 和 AOP 有冲突）
        // exclude 'androidx', 'com.google', 'com.squareup', 'org.apache', 'com.alipay', 'com.taobao', 'versions.9'
        // 只对以下包名做 AOP 处理
        include 'com.knight.kotlin'
        // 否则就会引发冲突，具体表现为：
        // 编译不过去，报错：java.util.zip.ZipException：Cause: zip file is empty
        // 编译能过去，但运行时报错：ClassNotFoundException: Didn't find class on path: DexPathList
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }


}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path:":module_welcome")
    implementation project(path:":module_home")
    implementation project(path:":module_wechat")
    implementation project(path:":module_main")
    implementation project(path:":module_mine")
    implementation project(path:":module_navigate")
    implementation project(path:":module_project")
    implementation project(path:":module_square")
    implementation project(path:":module_web")
    implementation project(path:":module_set")
    implementation project(path:":module_utils")
    implementation project(path:":module_course")
    implementation Dependencies.JetPack.HiltCore
    kapt Dependencies.JetPack.HiltApt
    kapt Dependencies.JetPack.HiltAndroidx
}